plugins {
    id 'kotlin-multiplatform'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'mirego.release' version '1.13'
    id 'mirego.publish' version '1.0'
}

repositories {
    google()
    jcenter()
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://jitpack.io' }
    maven { url('https://s3.amazonaws.com/mirego-maven/public') }
}

group 'com.mirego.trikot'

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm')
        fromPreset(presets.js, 'js')
        fromPreset(presets.iosArm64, 'iosArm64')
        fromPreset(presets.iosX64, 'iosX64')
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "com.mirego.trikot:foundation:$trikot_foundation_version"
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            }
        }
        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }
        jvmMain {
            dependencies {
                implementation "com.mirego.trikot:foundation-jvm:$trikot_foundation_version"
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
            }
        }
        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }
        jsMain {
            dependencies {
                implementation "com.mirego.trikot:foundation-js:$trikot_foundation_version"
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
            }
        }

        nativeMain {
            dependsOn(commonMain)
        }

        iosArm64Main {
            dependsOn(nativeMain)
            dependencies {
                implementation "com.mirego.trikot:foundation-iosarm64:$trikot_foundation_version"
            }
        }

        iosX64Main {
            dependsOn(nativeMain)
            dependencies {
                implementation "com.mirego.trikot:foundation-iosx64:$trikot_foundation_version"
            }
        }
    }
}

release {
   checkTasks = ['check']
   buildTasks = ['publish']
   updateVersionPart = 1
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}
