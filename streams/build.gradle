plugins {
    id 'com.android.library'
    id 'kotlin-multiplatform'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'mirego.release' version '2.0'
    id 'mirego.publish' version '1.0'
}

repositories {
    google()
    jcenter()
    mavenLocal()
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://jitpack.io' }
    maven { url('https://s3.amazonaws.com/mirego-maven/public') }
}

group 'com.mirego.trikot'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
    }
}

kotlin {
    android {
        publishAllLibraryVariants()
    }
    jvm()
    ios()
    iosArm32('iosArm32')
    tvos()
    js(IR) {
        browser()
    }

    sourceSets {
        all {
            languageSettings {
                useExperimentalAnnotation('kotlin.time.ExperimentalTime')
            }
        }

        commonMain {
            dependencies {
                implementation "com.mirego.trikot:trikotFoundation:$trikot_foundation_version"
            }
        }

        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }

        jvmMain {
            dependsOn commonMain
        }

        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }

        jsMain {
            dependsOn commonMain
        }

        jsTest {
            dependsOn commonTest
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-js'
            }
        }

        androidMain {
            dependsOn commonMain
            dependencies {
                implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
                implementation 'androidx.lifecycle:lifecycle-reactivestreams-ktx:2.3.0'
            }
        }

        androidTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }

        nativeMain {
            dependsOn commonMain
        }

        iosArm32Main {
            dependsOn nativeMain
        }

        iosArm64Main {
            dependsOn nativeMain
        }

        iosX64Main {
            dependsOn nativeMain
        }
        tvosArm64Main {
            dependsOn nativeMain
        }

        tvosX64Main {
            dependsOn nativeMain
        }
    }
}

release {
    checkTasks = ['check']
    buildTasks = ['publish']
    updateVersionPart = 2
}
