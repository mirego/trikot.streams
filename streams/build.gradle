plugins {
    id 'kotlin-multiplatform'
    id 'com.android.library'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'mirego.release' version '1.13'
    id 'mirego.publish' version '1.0'
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
    }
}

repositories {
    google()
    jcenter()
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://jitpack.io' }
    maven { url('https://s3.amazonaws.com/mirego-maven/public') }
}

group 'com.mirego.trikot'

kotlin {
    android {
        publishAllLibraryVariants()
    }

    targets {
        fromPreset(presets.jvm, 'jvm')
        fromPreset(presets.android, 'android')
        fromPreset(presets.js, 'js')
        fromPreset(presets.iosArm32, 'iosArm32')
        fromPreset(presets.iosArm64, 'iosArm64')
        fromPreset(presets.iosX64, 'iosX64')
        fromPreset(presets.tvosArm64, 'tvosArm64')
        fromPreset(presets.tvosX64, 'tvosX64')
    }
    sourceSets {
        all {
            languageSettings {
                useExperimentalAnnotation('kotlin.time.ExperimentalTime')
            }
        }

        commonMain {
            dependencies {
                implementation "com.mirego.trikot:trikotFoundation:$trikot_foundation_version"
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            }
        }
        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }
        jvmMain {
            dependencies {
                implementation "com.mirego.trikot:trikotFoundation-jvm:$trikot_foundation_version"
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
            }
        }
        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }
        jsMain {
            dependencies {
                implementation "com.mirego.trikot:trikotFoundation-js:$trikot_foundation_version"
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
            }
        }

        androidMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
            }
        }

        androidTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }

        nativeMain {
            dependsOn(commonMain)
        }

        iosArm32Main {
            dependsOn(nativeMain)
            dependencies {
                implementation "com.mirego.trikot:trikotFoundation-iosarm32:$trikot_foundation_version"
            }
        }

        iosArm64Main {
            dependsOn(nativeMain)
            dependencies {
                implementation "com.mirego.trikot:trikotFoundation-iosarm64:$trikot_foundation_version"
            }
        }

        iosX64Main {
            dependsOn(nativeMain)
            dependencies {
                implementation "com.mirego.trikot:trikotFoundation-iosx64:$trikot_foundation_version"
            }
        }
        tvosArm64Main {
            dependsOn(nativeMain)
            dependencies {
                implementation "com.mirego.trikot:trikotFoundation-tvosarm64:$trikot_foundation_version"
            }
        }

        tvosX64Main {
            dependsOn(nativeMain)
            dependencies {
                implementation "com.mirego.trikot:trikotFoundation-tvosx64:$trikot_foundation_version"
            }
        }
    }
}

release {
   checkTasks = ['check']
   buildTasks = ['publish']
   updateVersionPart = 1
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}
